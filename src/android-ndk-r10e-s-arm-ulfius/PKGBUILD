# Maintainer: Kuklin Istv√°n Alexander <istvan@codekuklin.com>
baselibname=ulfius
pkgname=android-ndk-r10e-s-arm-"$baselibname"
pkgver=2.6.5
pkgrel=1
pkgdesc="Web Framework to build REST APIs, Webservices or any HTTP endpoint in C language."
arch=('any')

depends=(
  'android-ndk-r10e-s-arm'
  'android-ndk-r10e-s-arm-gnutls'
  'android-ndk-r10e-s-arm-libmicrohttpd'
  'android-ndk-r10e-s-arm-jansson'
  'android-ndk-r10e-s-arm-curl'
  'android-ndk-r10e-s-arm-libidn2=2.0.5'
)

options=("!strip" "staticlibs")

source=(
  "$baselibname::git+https://github.com/babelouest/ulfius#tag=v$pkgver"
  "0001-Orcania-Yder-from-scratch-bug.patch"
)
md5sums=('SKIP'
         '515e851c539776309dfa57b7f97935bb')

prepare() {
    cd "$baselibname"
    git apply "$srcdir"/0001-Orcania-Yder-from-scratch-bug.patch
}

build() {
  cd "$baselibname"
  unset CFLAGS CXXFLAGS
  export LDFLAGS="$LDFLAGS -lm"
  cmake \
    -DCMAKE_TOOLCHAIN_FILE=/usr/ndk/r10e/standalone/armeabi/cmake.toolchain \
    -DCMAKE_INSTALL_PREFIX=/usr/ndk/r10e/standalone/armeabi/sysroot/usr/ \
    -DCMAKE_IGNORE_PATH="/usr/local/lib;/usr/lib" \
    -DWITH_JOURNALD=Off \
    -DSEARCH_YDER=On \
    -DSEARCH_ORCANIA=On \
    -DBUILD_STATIC=On
  make 
}

package() {
  cd "$srcdir"/"$baselibname"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/ndk/r10e/standalone/armeabi/sysroot/usr/share/info
}

# Unused options:

url=""
groups=()
makedepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
install=
changelog=
noextract=()
